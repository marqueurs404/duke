plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

group 'seedu.duke'
version '0.1.0'

repositories {
    mavenCentral()
}

run {
    standardInput = System.in
}

application {
    mainClassName = "core.Duke"
}

task runGui(type:JavaExec) {
    main = "core.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}

checkstyle {
    toolVersion = '8.23'
}

shadowJar {
    mergeServiceFiles()
}

task shadowJarGui(type: Jar, dependsOn: shadowJar) {
    group = "shadow"
    description = "Builds a jar of the gui version of duke"
    archiveBaseName = "dukeGui"
    archiveVersion = "0.2.0"
    archiveClassifier = null
    archiveAppendix = null
    manifest.attributes 'Main-Class': 'core.Launcher'
    from zipTree(shadowJar.archivePath)
}

task shadowJarCli(type: Jar, dependsOn: shadowJar) {
    group = "shadow"
    description = "Builds a jar of the cli version of duke"
    archiveBaseName = "dukeCli"
    archiveVersion = "0.2.0"
    archiveClassifier = null
    archiveAppendix = null
    manifest.attributes 'Main-Class': 'core.Duke'
    from zipTree(shadowJar.archivePath)
}


javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}
